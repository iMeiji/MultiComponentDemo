if (isBuildModule.toBoolean()) {
    apply plugin: 'com.android.application'
} else {
    apply plugin: 'com.android.library'
}

apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'

android {

    defaultConfig {
        if (isBuildModule.toBoolean()) {
            applicationId "com.meiji.multicomponentdemo.${this.name}"
        }

        // ARouter 配置
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [moduleName: project.getName()]
            }
        }

        // ARouter 配置(Kotlin 写法)
        kapt {
            arguments {
                arg("moduleName", project.getName())
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    resourcePrefix "${project.name}_" //给 Module 内的资源名增加前缀, 避免资源名冲突

    sourceSets {
        main {
            // 设置两套 AndroidManifest
            if (isBuildModule.toBoolean()) {
                manifest.srcFile 'src/main/AndroidManifestDebug.xml'
            } else {
                manifest.srcFile 'src/main/AndroidManifest.xml'
                // release模式下排除debug文件夹中的所有Java和Kotlin文件
                java {
                    exclude 'debug/**'
                }
                kotlin {
                    exclude 'debug/**'
                }
            }
        }
    }
}

dependencies {
    api fileTree(include: ['*.jar'], dir: 'libs')
    api project(':commonBase')
    api "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    kapt lib_arouter_compiler
}